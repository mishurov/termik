apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'


android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "uk.co.mishurov.termik2"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 2
        versionName "2.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        ndk {
            //abiFilters "armeabi-v7a"
            //abiFilters "arm64-v8a"
            //abiFilters "x86"
            abiFilters "x86", "x86_64", "armeabi-v7a", "arm64-v8a"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }
    sourceSets {
        main {
            java {
                srcDir 'src/main/java'
            }
            manifest.srcFile 'src/main/AndroidManifest.xml'
            res.srcDirs = [
                'src/main/res'
            ]
            jniLibs.srcDirs = [
                '../downloads/OpenCV-android-sdk/sdk/native/libs',
            ]
            assets.srcDirs = ['assets']
        }
    }
    splits {
        abi {
            enable true
            reset()
            include "x86", "x86_64", "armeabi-v7a", "arm64-v8a"
            universalApk false
        }
    }
}

ext.abiCodes = ["armeabi-v7a": 1, "arm64-v8a": 2, "x86": 3, "x86_64": 4]


import com.android.build.OutputFile

android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        def baseAbiVersionCode =
                project.ext.abiCodes.get(output.getFilter(OutputFile.ABI))
        if (baseAbiVersionCode != null) {
            output.versionCodeOverride =
                            baseAbiVersionCode * 1000 + variant.versionCode
        }
    }
}


project.ext.DOWNLOAD_DIR = project.rootDir.toString() + '/downloads'
project.ext.ASSET_DIR = projectDir.toString() + '/assets'
apply from: "download.gradle"


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation "com.android.support:appcompat-v7:26.0.0"
    implementation 'com.google.vr:sdk-base:1.101.0'

    implementation 'org.tensorflow:tensorflow-android:1.4.0'
    implementation project(':opencv')

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
